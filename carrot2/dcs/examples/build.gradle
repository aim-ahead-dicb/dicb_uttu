
import org.apache.tools.ant.filters.*

apply plugin: 'java'

dependencies {
  implementation project(':core')
  implementation project(':dcs:model')

  implementation deps.jackson.databind
  implementation deps.httpclient
  implementation deps.carrotsearch.console.launcher
}

configurations {
  distribution
}

task buildDistribution(type: Sync) {
  def antPrj = new org.apache.tools.ant.Project()
  afterEvaluate {
    replaceTokens.each { k, v -> antPrj.setProperty(k.toString(), v.toString()) }
  }

  inputs.properties replaceTokensTrigger

  dependsOn configurations.runtimeClasspath

  doFirst {
    antPrj.setProperty("version.jackson-databind", getResolvedVersion(
            deps.jackson.databind.get(),
            configurations.runtimeClasspath
    ))
    antPrj.setProperty("version.httpclient", getResolvedVersion(
            deps.httpclient.get(),
            configurations.runtimeClasspath
    ))
    antPrj.setProperty("version.launcher", getResolvedVersion(
            deps.carrotsearch.console.launcher.get(),
            configurations.runtimeClasspath
    ))
  }

  from(project.projectDir, {
    include 'src/**'
    exclude 'src/main/assembly/**'
  })

  from(file("src/main/assembly"), {
    include '**/*'
    filteringCharset = 'UTF-8'
    filter(ExpandProperties, project: antPrj)
  })

  into "$buildDir/distribution"
}

artifacts {
  distribution file("$buildDir/distribution"), {
    builtBy tasks.buildDistribution
  }
}


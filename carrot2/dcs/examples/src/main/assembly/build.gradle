
apply plugin: 'java'

repositories {
  mavenCentral()

  def localRepo = file('../../artifacts')
  if (localRepo.isDirectory()) {
    maven {
      name 'local'
      url localRepo
    }
  }
}

dependencies {
  implementation "org.carrot2:carrot2-core:${product.version}"
  implementation "org.carrot2.dcs:carrot2-dcs-model:${product.version}"

  implementation "com.fasterxml.jackson.core:jackson-databind:${version.jackson-databind}"
  implementation "org.apache.httpcomponents:httpclient:${version.httpclient}"
  implementation "com.carrotsearch.console:launcher:${version.launcher}"
}

jar {
  doFirst {
    manifest {
      attributes("Main-Class": "com.carrotsearch.console.launcher.Launcher")
      attributes("Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '))
    }
  }
}

task copyJars(type: Sync) {
  from(configurations.runtimeClasspath)
  from(jar)

  into "$buildDir/assembled"
}

assemble.dependsOn copyJars
